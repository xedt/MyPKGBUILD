# Maintainer: Wai <waicool20@gmail.com>
pkgname=huiontablet
pkgver=v15.0.0.164
pkgrel=1
pkgdesc="Official Huion tablet drivers"
arch=('x86_64')
url="https://www.huion.com/download/"
license=('unknown')
source=("https://driverdl.huion.com/driver/GT1302-1602-1562-2401/HuionTablet_LinuxDriver_v15.0.0.164.x86_64.tar.xz")
md5sums=("SKIP")

prepare() {
  cd "${srcdir}"

  # Modify install script with our dirs
  sed -E -i "s|(sysRuleDir=).*|\1$pkgdir/usr/lib/udev/rules.d|" install.sh
  sed -E -i "s|(sysAppDir=).*|\1$pkgdir/usr/lib/|" install.sh
  sed -E -i "s|(sysDesktopDir=).*|\1$pkgdir/usr/share/applications|" install.sh
  sed -E -i "s|(sysAppIconDir=).*|\1$pkgdir/usr/share/icons|" install.sh
  sed -E -i "s|(sysAutoStartDir=).*|\1$pkgdir/etc/xdg/autostart|" install.sh

  sed -E -i "s|killall.*||" install.sh
  sed -E -i "s|sudo ||" install.sh
  sed -E -i "s|/usr/lib/huiontablet/res|$pkgdir\0|" install.sh

  # Remove stuff after this line
  sed -E -i "/#Copy config files/Q" install.sh

  # Remove the line after "#Uninstall HUION driver first" to stop running uninstall.sh
  sed -E -i "/#Uninstall HUION driver first/{n;d;}" install.sh
}

package() {
  # Fix for "Can't translate pathname" error in CJK language environment
  export LC_ALL=C.UTF-8

  cd "${srcdir}"

  # Make directories
  grep "sys.*Dir=" install.sh | sed -E 's/.*=(.*)/\1/' | tr -d '"' | xargs mkdir -p

  # Invoke install script
  sh install.sh

  # Set the permission of "/usr/lib/huiontablet" folder to 777
  # Fix for not being able to change button or area mapping
  chmod -R +0777 $pkgdir/usr/lib/huiontablet
}
